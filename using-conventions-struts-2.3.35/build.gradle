buildscript {
  ext {
    strutsVersion = "2.3.35"
    log4jVersion = "2.8.2"
    asmVersion = "5.1"
  }
}

plugins {
  id("com.github.ben-manes.versions") version "0.27.0"
  id("io.franzbecker.gradle-lombok") version "3.2.0" apply false
  id("io.spring.dependency-management") version "1.0.8.RELEASE" apply false
}

allprojects {
  apply(plugin: "base")
  apply(plugin: "maven")

  group = "daggerok"
  version = "0.0.2"
}

subprojects {
  apply(plugin: "java")
  sourceCompatibility = JavaVersion.VERSION_1_7
  targetCompatibility = JavaVersion.VERSION_1_7

  repositories {
    mavenCentral()
    maven { url("http://repo.maven.apache.org/maven2") }
    jcenter()
  }

  apply(plugin: "io.franzbecker.gradle-lombok")
  lombok.version = "1.18.10"

  apply(plugin: "io.spring.dependency-management")

  dependencyManagement {
    imports {
      mavenBom("org.apache.struts:struts2-bom:$strutsVersion")
      mavenBom("org.apache.logging.log4j:log4j-bom:$log4jVersion")
    }
  }

  dependencies {
    compileOnly("javax:javaee-api:7.0")
  }
}

wrapper {
  gradleVersion = "5.6.4"
}

defaultTasks("clean", "build")
